Suika 2 コマンドリファレンス
============================

## @bg

背景を変更します。背景の変更後、キャラクタはステージ上からいなくなります。

* 使い方1: `sample.png`を1.5秒かけて背景にセットします。
```
@bg sample.png 1.5
```

* 使い方2: `sample.png`を即座に背景にセットします。
```
@bg sample.png
```

* 使い方3: `sample.png`を1.5秒かけてカーテンフェードで背景にセットします。
```
@bg sample.png 1.5 c
```

## @bgm

BGMを再生します。BGMファイルはフォルダ`bgm`に格納されている必要があります。再生可能なファイル形式は、44.1kHzのOgg Vorbisのみです。

* 使い方1: sample.oggを再生します。
```
@bgm sample.ogg
```

* 使い方2: BGMを停止します。
```
@bgm stop
```

* 応用例: BGMを2秒かけてフェードアウトします。
```
@vol 0 2
@wait 2
@bgm stop
```

## @ch

キャラクタを変更します。キャラクタの位置は、上位レイヤから順に、中央前面(center)、右(right)、左(left)、中央背面(back)の4つがあります。

* 使い方1: `sample.png`を0.5秒かけて中央前面にセットします。
```
@ch center sample.png 0.5
```

* 使い方2: `center`はcと省略して構いません。
```
@ch c sample.png 0.5
```

* 使い方3: `none`を指定するとキャラクタを退場させます。
```
@ch c none 0.5
```

* 使い方4: 時間を指定しないと即座に表示されます。
```
@ch c sample.png
```

## @click

クリックされるのを待ちます。待っている間、メッセージボックスは非表示となります。

* 使い方:
```
@click
```

## @goto

ラベルにジャンプします。ループを作成するときや、ループから抜け出す際に使用します。

* 使い方: ラベル`abc`にジャンプします。
```
:abc
ここに何らかの処理やメッセージを記述します。
@goto abc
```

## @if

条件式が成立するときラベルにジャンプします。
利用できる演算子は`>`,`>=`,`==`,`<=`,`<`,`!=`です。

* 使い方1: 変数1が10以上であればラベル"abc"にジャンプします。左辺値は変数名である必要があります。右辺値には整数か変数名が使用できます。
```
@if $1 >= 10 abc
変数$1が10未満のときの処理を記述します。
:abc
```

## @menu

画像を2枚使ってメニューを表示します。ボタンを4つまで作成できます。基本的に1枚目の画像が表示されますが、ボタンがマウスでポイントされている間は、そのボタンの領域だけ2枚目の画像が表示されます。メニュー画面は右クリックでキャンセルできません。

* 使い方:
```
@menu menu.png menu-selected.png START 640 480 240 120
:START
```

## ラベル

ジャンプ先となるラベルを作成します。

* 使い方:
```
:ジャンプ先
何かメッセージを表示する
@goto ジャンプ先
```

## メッセージ

メッセージボックスにテキストを出力します。\nで改行できます。$数字で変数の値を出力できます。

* 使い方:
```
こんにちは、世界。
```

## @load

スクリプトファイルにジャンプします。スクリプトファイルはフォルダ`txt`の中に存在する必要があります。

* 使い方: スクリプトファイル`001.txt`にジャンプします。
```
@load 001.txt
```

## @retrospect

CG鑑賞モードを実行します。@menuと同様に2枚の画像を使用します@。menuとの違いは、作成できるボタンの数が12個までであることと、それぞれのボタンに変数を指定して、変数の値が0のときはボタンを隠すことです。また、@menuと異なり右クリックでキャンセルが可能です。

引数が多いです。先頭から順に、背景画像、選択時に上書きされる画像、変数の値が0のときに背景を塗りつぶす色のR、G、B、各ボタンの幅、各ボタンの高さと続きます。以降、ボタンの数だけ、ジャンプ先ラベル、チェックする変数、ボタンのX座標、ボタンのY座標、の４つが続きます。

* 使い方:
```
@retrospect menu.png menu-selected.png 0 0 0 320 240 PICTURE1 $1 320 240
キャンセルされました
@goto END
:PICTURE1
@bg picture1.png 1.0
@click
:END
```

## @se

SEを再生します。SEファイルはフォルダ`se`に格納されている必要があります。再生可能なファイル形式は、44.1kHzのOgg Vorbisのみです。

* 使い方:
```
@se click.ogg
```

## @selectコマンド

選択肢を表示してラベルにジャンプします。選択肢の数は３つに固定されています。

* 使い方:
```
@select label1 label2 label3 おはよう こんにちは こんばんは
:label1
おはよう
@goto end
:label2
こんにちは
@goto end
:label3
こんばんは
:end
```

## セリフ

メッセージボックスにセリフを出力します。また、名前ボックスに名前を出力します。ボイスを再生することができます。ボイスファイルは、フォルダ"cv"の中に存在する必要があります。ボイスファイルの形式は、44.1kHzのOgg Vorbisである必要があります。メッセージと同様に、\nで改行できます。$数字で変数の値を出力できます。

* 使い方1: セリフを出力します。
```
*名前*こんにちは、世界。
```

* 使い方2: ボイスを再生して、セリフを出力します。
```
*名前*001.ogg*こんにちは、世界。
```

* 使い方3: 指定したビープ音をリピート再生して、セリフを出力します。
```
*名前*@beep.ogg*こんにちは、世界。
```

## @set

変数に値をセットします。セーブデータごとに値が異なるローカル変数は、`$0`から`$9999`までの10000個を利用できます。変数の初期値は`0`です。すべてのセーブデータで共通の値となるグローバル変数は、`$10000`から`$10999`までの1000個を利用できます。利用できる演算子は、`=`, `+=`, `-=`, `*=`, `/=`, `%=`です。

* 使い方1: 変数`$0`に値`1`を代入します
```
@set $0 = 1
```

* 使い方2: 変数`$10`に値`23`を加算します
```
@set $10 += 23
```

* 使い方3: 変数`$0`にランダムな値(0以上2147483647まで)を代入します
```
@set $0 = $RAND
```

* 使い方4: 変数`$1`に変数`$2`を加算します
```
@set $1 += $2
```

## @switch

最大2階層のコマンド選択肢を表示します。親コマンドと子コマンドがそれぞれ8個ずつで、合計で最大64分岐を記述できます。1階層のメニューとして利用することも可能です。

* 使い方1: 1階層で2つの選択肢を表示します
```
@switch 親選択肢1 親選択肢2 * * * * * * LABEL1 * * * * * * * * * * * * * * * LABEL2 * * * * * * * * * * * * * * *
```

* 使い方2: 2階層で4つの選択肢を表示します
```
@switch 親選択肢1 親選択肢2 * * * * * * LABEL1 子選択肢1 LABEL2 子選択肢2 * * * * * * * * * * * * LABEL3 子選択肢3 LABEL4 子選択肢4 * * * * * * * * * * * *
```

## @vol

時間を指定してボリュームを調整します。調整できる出力は、BGM(`bgm`)、ボイス(`voice`)、SE(`se`)の3つの入力です。

* 使い方1: BGMの音量を1秒かけて0.5にします
```
@vol bgm 0.5 1.0
```

* 使い方2: `bgm`は`b`と省略できます。
```
@vol b 0.5 1.0
```

## @wait

時間を指定して待機します。途中、キーボードやマウスの入力があれば待機を終了します。待機中、メッセージボックスは表示されません。

* 使い方: 5秒待ちます。
```
@wait 5.0
```
