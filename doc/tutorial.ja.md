Suika2 チュートリアル
======================

Suika2の世界へようこそ！

このページでは、Suika2を使ってアドベンチャーゲーム・ノベルゲームを作成する方法を説明します。

内容はとってもかんたんです。用意はいいですか？さあ、始めましょう！

## 準備

ここではWindowsを使います。

まずはじめに、[ダウンロード](https://suika2.com/dl.html)したSuika2のZIPファイルを展開すると、下記のようなフォルダとファイルが作成されます。

これは、Suika2の実行ファイルとサンプルゲームが一緒になったものです。

* `suika2/`
    * `bg/` 背景画像を格納するフォルダです
    * `bgm/` BGMを格納するフォルダです
    * `cg/` メッセージボックス等のシステム画像を格納するフォルダです
    * `ch/` キャラクタ画像を格納するフォルダです
    * `conf/` 設定ファイルを格納するフォルダです
    * `cv/` ボイスを格納するフォルダです
    * `font/` フォントを格納するフォルダです
    * `sav/` セーブデータを格納するフォルダです
    * `se/` 効果音を格納するフォルダです
    * `tool/` ファイルをパッケージにまとめるツールのフォルダです
    * `txt/` スクリプトを格納するフォルダです
    * `suika.exe` Windows用アプリです

このサンプルゲームに変更を加えてチュートリアルを進めていきます。

まだサンプルゲームをプレイしていなければ、イメージをつかむために、一度`suika.exe`を実行してプレイしてみてください。

## スクリプトを開く

サンプルゲームのフォルダ構成を確認していただいたところで、次に移ります。

`txt`フォルダを開くと、`init.txt`というファイルがあります。

このファイルが、ゲームの起動後、最初に読み込まれるスクリプト(Suika2への指示書)です。

`init.txt`をダブルクリックして開いていただければ、ただの文字の並び(テキストファイル)であることがわかるはずです。

## 背景を表示する

`init.txt`がテキストファイルであることを確認していただいたところで、次は背景の表示に移ります。

ここで一度、`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@bg roof.png 1.0
@click
```

保存したら、`suika.exe`を実行してゲームを起動します。

屋上の背景が表示され、クリックを行うと、終了するはずです。

`@bg`というのが背景を表示する命令(コマンド)で、`roof.png`は`bg`フォルダの中のファイル名、`1.0`は画面の表示が完了するまで(フェードイン)の時間(秒)です。

## キャラクタを表示する

背景を表示できたところで、次はキャラクタの表示に移ります。

`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@bg roof.png 1.0
@ch center 001-fun.png 1.0
@click
```

保存したら、ゲームを実行します。

屋上の背景が表示され、続いてキャラクタが表示され、クリックを行うと、終了するはずです。

`@ch`というのがキャラクタを表示するコマンドで、`center`は表示位置(中央)、`001-fun.png`は`ch`フォルダの中のファイル名、`1.0`はフェードインの時間(秒)です。

## メッセージとセリフを表示する

背景、キャラクタを表示できたところで、次はメッセージとセリフの表示に移ります。

`init.txt`の中身をすべて削除し、次の内容を貼り付け、保存してみてください。

```
@bg roof.png 1.0
@ch center 001-fun.png 1.0
メッセージボックスにテキストを表示するのが「メッセージ」です。
*みどり*キャラクタの名前を付けたメッセージが「セリフ」です。
*みどり*011.ogg*セリフではボイスを再生することができます。
```

保存したら、ゲームを実行します。

先ほどのキャラクタが表示されたあと、メッセージとセリフが表示されるはずです。

メッセージとして認識される行は、普通の文章の行(先頭が@や\*や:ではない行)です。

セリフとして認識される行は、先頭が\*で始まる行です。

## BGMを再生する

メッセージ、セリフを表示できたところで、次はBGMの再生に移ります。

`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@bgm 01.ogg
@bg roof.png 1.0
@ch center 001-fun.png 1.0
BGMが再生されます。
```

保存したら、ゲームを実行します。

BGMが再生され、先ほどのキャラクタが表示されるはずです。

`@bgm`はBGM再生コマンドを表し、`01.ogg`は`bgm`フォルダのファイル名を表します。

再生できるファイルは、44.1kHzのOgg Vorbis形式です。ステレオ、モノラルのどちらでも構いません。

## 選択肢を表示する

BGMの再生ができたところで、次は選択肢の表示に移ります。

`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@bg roof.png 1.0
@ch center 001-fun.png 1.0
さて、学校へ行くか。
@select label1 label2 label3 歩いて行こう 自転車で行こう やっぱりサボろう
:label1
歩いて行くことにした。
@goto end
:label2
自転車で行くことにした。
@goto end
:label3
今日は休むことにした。
:end
```

保存したら、ゲームを実行します。

3つの選択肢が表示されるはずです。

`@select`は選択肢コマンドを表し、`label1` `label2` `label3`は選択肢が選ばれたときにジャンプする先のラベルです(後述)。その後ろに選択肢が続きます。

`:label1`は、ジャンプ先として指定されるラベルです。ラベルの行は、ゲームの見た目の上では何も実行されません。

## フラグを立てて分岐する

選択肢の表示ができたところで、次はフラグを立てて分岐する処理に移ります。

これができればノベルゲームがひとまず形になります。

`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@bg roof.png 1.0
@ch center 001-fun.png 1.0
さて、学校へ行くか。
@select label1 label2 label3 歩いて行こう 自転車で行こう やっぱりサボろう
:label1
歩いて行くことにした。
@set $1 = 1
@goto next
:label2
自転車で行くことにした。
@goto next
:label3
今日は休むことにした。
:next
@if $1 == 0 end
お金を拾った。
:end
```

保存したら、ゲームを実行します。

「歩いて行こう」を選択した場合だけ、「お金を拾った。」が表示されるはずです。

ここでは`@set`と`@if`の2つのコマンドを組み合わせています。

`@set`は、変数設定コマンドを表します。続く`$1`は、変数番号1に値を格納することを意味します。続く`=`は、単純な代入であることを意味します(他にも計算ができます)。続く`1`は、整数の1を代入することを意味します。変数の初期値は`0`ですので、「歩いて行こう」が選択されたときだけ、変数`$1`の値が`1`になります。

`@if`は、条件分岐コマンドを表します。`$1 == 0`は、変数`$1`の値が`0`の場合、という条件を意味します。続く`end`は、条件に一致した場合はラベル`end`にジャンプする、という意味を表します。変数の初期値は`0`ですので、結果として、「歩いて行こう」を選択した場合だけ、「お金を拾った。」が表示されることになります。

変数の使い方は0/1のフラグだけではなく、好感度などの数値を持つこともできます。詳細はコマンド・リファレンスの`@set`と`@if`をご覧ください。ただ、スクリプトをシンプルに保つためには、なるべく0/1のフラグとして運用する方が望ましいと思います。

## メニュー画面を表示する

シナリオの分岐ができたところで、ちょっとステップアップして、タイトル画面やメニュー画面の作成方法を説明します。

`init.txt`の中身をすべて削除して、次の内容を貼り付け、保存してみてください。

```
@menu menu.png menu_selected.png START 2 495 314 192 LOAD 316 494 322 203 CONFIG 640 492 317 204 QUIT 960 497 317 201
:START
:LOAD
:CONFIG
:QUIT
```

保存したら、とりあえずゲームを実行してみてください。

サンプルゲームのタイトル画面が表示されるはずです。詳しく見ていきます。

`@menu`コマンドは、画像を2枚使ってメニュー画面を表示するコマンドです。

`@menu menu.png menu_selected.png`とありますが、これは`bg`フォルダの`menu.png`と`menu_selected.png`を使うという意味です。この2つのファイルを見てみてください。`menu.png`は、画面内のボタンがマウスでポイントされていない時の画像です。また、`menu_selected.png`が、画面内のボタンがマウスでポイントされている時の画像です。

`@menu menu.png menu_selected.png`に続く`START 2 495 314 192`ですが、これはボタンを作成しています。ボタンがクリックされたときのジャンプ先ラベルが`START`です。その後ろは、座標(2, 495)に314x192ピクセルのボタンを作成する、という意味を表しています。

## スクリプトファイルを分割する

シナリオの分量が大きくなってくると、1つのスクリプトファイルで書くのが大変になります。

そこで、シナリオの章やシーンごとにスクリプトを分割する方法があります。

`init.txt`のコピーを作成して、`second.txt`と名付けてください。

`init.txt`と`second.txt`の中身をすべて削除し、次の内容を貼り付け、保存してみてください。

* init.txt
```
@bg roof.png 1.0
@ch center 001-fun.png 1.0
init.txtの中にいます。
@load second.txt
```

* second.txt
```
second.txtに移動しました。
```

保存したら、ゲームを実行してみてください。

second.txtに移動できたことがわかるはずです。

@loadは、指定されたスクリプトファイルに移動するためのコマンドです。

ここで気をつけたいこととして、ラベルはスクリプトファイル内でのみ有効ですので、ジャンプ前のスクリプトファイルで使っていたラベルは、ジャンプ後のスクリプトファイルからは使うことができません。逆に言うと、古いスクリプトファイルで定義したのと同じ名前のラベルを作成することができます。

## ファイルを1つのパッケージにする

ほとんどの方がそうだと思いますが、素材ファイルがそのまま見えてしまうのが嫌だという場合、すべてのファイルをパッケージ(1つのファイル)にまとめることができます。

1. toolフォルダに入っているpackage-win.exeを、Suika2の実行ファイルがあるフォルダにコピーしてください
2. 上でコピーしたファイルをダブルクリックで実行すると、パッケージファイルdata01.arcができあがります
3. 配布ファイルには、Suika2の実行ファイル、data01.arcの2点があれば動作します
4. そのほかのbg, bgm, cg, ch, conf, cv, font, se, txtのフォルダは、配布ファイルに含める必要はありません
5. パッケージとふつうのファイルの両方がある場合、ふつうのファイルが優先的に利用されます

## 基本はこれでOK

こまかい演出機能はのちのち覚えていくとして、まずはこれでゲームが作れます。

以上でチュートリアルは終わりです。おつかれさまでした。
