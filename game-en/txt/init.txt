###
### Logo
###

@bg white.png 0
@ch c suika.png 1.0
@se suika.ogg
@wait 2.0
@ch c none 1.0

###
### Menu
###

@vol bgm 1.0
@bgm 00.ogg
@bg menu.png 1.0

:MENU
@menu menu.png menu_selected.png START 2 495 314 192 LOAD 316 494 322 203 CONFIG 640 492 317 204 QUIT 960 497 317 201

:START
@se click.ogg
@goto SCENE1

:LOAD
@se click.ogg
@goto $LOAD
@goto MENU

:CONFIG
@se click.ogg
@bg config.png 1.0
@goto CONFIG_SCREEN

:QUIT
@se click.ogg
@goto END

###
### Scene 1 (Mixture of Midori's lines and monologues)
###

:SCENE1

@vol bgm 0.0 1.0
@bg white.png 1.0
@bgm cicada.ogg
@vol bgm 1.0 1.0
@bg roof.png 1.0
@ch c 001-fun.png 1.0
*???*Oh, what a nice breeze...

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(I'm on the roof of my school. I've skipped the remedial class.)

@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*???*But it's OK, they don't call the roll.

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(There's no building near the school. I can see the coast line a short distance away. This is a country town in Hokkaido.)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*???*Are you asking me why I skipped the remedial class?

@vol bgm 0.5 1.0
@ch c none 1.0
*???*(Oh, I forgot to mention, I'm Midori. I'm a high school student in this country town. My parents are water melon farmers.)

@vol bgm 1.0 1.0
@ch c 001-angry.png 1.0
*Midori*It's because...

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(Oh, I have some important things to say. The reason why I'm here today is...)

@vol bgm 1.0 1.0
@ch c 001-normal.png 1.0
*Midori*I want you to...

@vol bgm 0.0 1.0
@ch c 001-happy.png 1.0
*Midori*I want you to know about Suika2!

###
### Scene2 (Greetings on the roof)
###

@bgm 02.ogg
@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*Midori*Hello! Thank you for using Suika2!
*Midori*As you might know, Suika2 is a visual novel authoring tool.
*Midori*It's free and runs on both Windows and Mac. Isn't it awesome?

@ch c 001-fun.png 1.0
*Midori*The author says he wants you to ask questions. Do you have any questions for him?
@ch c none 1.0

###
### Scene3 (Be asked questions on the roof)
###

:LOOP1
@select L1_ADVANTAGE L1_ENV L1_NEXT "What's the advantages?" "What are system requirements?" Next

:L1_ADVANTAGE
Suika2 has three advantages.
First of all, portability. We have Windows app, Mac app and Web browser version. All of them work just the same way.
Game developers don't need to bother about the users' OSes.
In addition, game developer can use favorite OS to develop a game.
Second, simplicity. Anyone can start writing scripts easily.
Last but not least, freedom. You can use Suika2 commercially for free.

@set $1 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, wow! I didn't know that. It's so cool.
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP1

:L1_ENV
For Windows, we support Windows 8.1 and later. We also support the latest Windows 10 tablets.
For Mac, we support macOS 10.9 and later. We surely support the latest Big Sur and M1 chip.
For Web browsers, we support Chrome, Safari, Firefox and Edge.
In addition, Suika2 runs on Android and Linux. Suika2 was written on Linux initially.
@set $2 = 1
@ch c 001-fun.png 1.0
*Midori*Yes! That's right.
*Midori*The author says he wants you to ask more questions.
@ch c none 1.0
@goto LOOP1

:L1_NEXT

###
### Scene4 (Be asked questions on the coast)
###

@ch c 001-fun.png 1.0
*Midori*Shall we step out and have a walk. Where do we go? Well, let's go to the coast.

@vol bgm 0.0 1.0
@bg coast.png 1.0 c
@vol bgm 1.0 0.0
@bgm coast.ogg
@ch c 001-happy.png 1.0
*Midori*Do you have any further questions?
@ch c none 1.0

:LOOP2
@select L2_OLD L2_CUSTOMIZE L2_NEXT "Do you have Suika1?" "Can I customize it?" Next

:L2_OLD
Versions before 2.0.0 was named Suika Studio. The author developed it when he was a high school student.
At 2.0.0, he removed the visual editor and "Studio" naming.
@set $3 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, I see. I didn't know that.
*Midori*The author says he wants you to ask more questions.
@ch c none 1.0
@goto LOOP2

:L2_CUSTOMIZE
Basically you can customize the engine with relatively little effort. For example, font, text box and save screen are customizable.
If you have any items that you want to customize but you\ncan't do, please contact the author.
@set $4 = 1
@ch c 001-fun.png 1.0
*Midori*Oh, yes!
*Midori*Do you have any other questions?
@ch c none 1.0
@goto LOOP2

:L2_NEXT

###
### Scene5
### (Last scene on the coast/Mixture of Midori's lines and monologues)
###

@ch c 001-fun.png 1.0
*Midori*Thanks for asking a lot today.

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(I just wanted you to know about Suika2...)

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*Midori*If you have any questions, send me an email to\nmidori@luxion.jp anytime.

@vol bgm 0.5 1.0
@ch c none 1.0
*Midori*(Don't hesitate to send me an email. I'll be waiting for your comment.)

@vol bgm 0.0 1.0
@ch c 001-fun.png 1.0
*Midori*See you again!

###
### Scene6 (Added bonus)
###

@if $1 != 1 END
@if $2 != 1 END
@if $3 != 1 END
@if $4 != 1 END
Thank you for selecting all the options.
@goto END

###
### Config Screen
###

:CONFIG_SCREEN
@menu config.png config_selected.png BGM_STOP 468 170 149 62 VOICE_STOP 468 256 149 62 SE_STOP 468 340 149 62 BGM_MEDIUM 655 170 237 62 VOICE_MEDIUM 655 256 237 62 SE_MEDIUM 655 340 237 62 BGM_MAX 933 170 127 62 VOICE_MAX 933 256 127 62 SE_MAX 933 340 127 62 CONFIG_OK 524 598 219 73

:BGM_STOP
@vol BGM 0
@goto CONFIG_SCREEN

:VOICE_STOP
@vol VOICE 0
@goto CONFIG_SCREEN

:SE_STOP
@vol SE 0
@goto CONFIG_SCREEN

:BGM_MEDIUM
@vol BGM 0.5
@goto CONFIG_SCREEN

:VOICE_MEDIUM
@vol VOICE 0.5
@se 025.ogg voice
@goto CONFIG_SCREEN

:SE_MEDIUM
@vol SE 0.5
@se click.ogg
@goto CONFIG_SCREEN

:BGM_MAX
@vol BGM 1.0
@goto CONFIG_SCREEN

:VOICE_MAX
@vol VOICE 1.0
@se 025.ogg voice
@goto CONFIG_SCREEN

:SE_MAX
@vol SE 1.0
@se click.ogg
@goto CONFIG_SCREEN

:CONFIG_OK
@se click.ogg
@bg menu.png 1.0
@goto MENU

###
### Quit Logo
###

:END
@bg white.png 1.0
@ch c luxion.png 0.5
@wait 1.0
@ch c none 1.0
