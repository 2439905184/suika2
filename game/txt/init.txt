###
### ロゴ
###

@bg white.png 0
@ch c suika.png 1.0
@se suika.ogg
@wait 2.0
@ch c none 1.0

###
### メニュー
###

@vol bgm 1.0
@bgm 00.ogg
@bg menu.png 1.0

:MENU
@menu menu.png menu_selected.png START 27 485 317 193 LOAD 357 493 318 213 QUIT 678 492 315 191

:START
@se click.ogg
@goto SCENE1

:LOAD
@se click.ogg
@goto $LOAD
@goto MENU

:QUIT
@se click.ogg
@goto END

###
### シーン1 (セリフとモノローグの交錯)
###

:SCENE1

@vol bgm 0.0 1.0
@bg white.png 1.0
@bgm cicada.ogg
@vol bgm 1.0 1.0
@bg roof.png 1.0
@ch c 001-fun.png 1.0
*？？？*001.ogg*あー、気持ちいい風……。

@vol bgm 0.5 1.0
@ch c none 1.0
*？？？*002.ogg*（私は今、夏休みの補習を抜け出して、学校の屋上に出ている。）

@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*？？？*003.ogg*補習は出欠取らないから、別にいいんだ～。

@vol bgm 0.5 1.0
@ch c none 1.0
*？？？*004.ogg*（辺りにビルは見あたらず、少し離れたところには海岸線が見える。ここは北海道の田舎町だ。）

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*？？？*005.ogg*どうして補習を抜け出したのかって？

@vol bgm 0.5 1.0
@ch c none 1.0
*？？？*006.ogg*（あ、言い忘れてた。私の名前はみどり。この田舎町で女子高生をやってる。家は農家だよ。）

@vol bgm 1.0 1.0
@ch c 001-angry.png 1.0
*みどり*007.ogg*それはね……

@vol bgm 0.5 1.0
@ch c none 1.0
*みどり*008.ogg*（そうだった。とても大切なことがあったんだ。今日私がここにいる理由は……。）

@vol bgm 1.0 1.0
@ch c 001-normal.png 1.0
*みどり*009.ogg*君に……

@vol bgm 0.0 1.0
@ch c 001-happy.png 1.0
*みどり*010.ogg*君に……Suika 2のことを知ってもらうためだよ！

###
### シーン2 (屋上であいさつする)
###

@bgm 02.ogg
@vol bgm 1.0 1.0
@ch c 001-happy.png 1.0
*みどり*011.ogg*こんにちは！Suika 2を使ってくれてありがと！
*みどり*012.ogg*知ってると思うけど、Suika 2は、アドベンチャーゲーム制作ツールだよ。
*みどり*013.ogg*自由に使えて、PCでもMacでも動くなんて、すごくない？

@ch c 001-fun.png 1.0
*みどり*014.ogg*作者が質問してほしいんだって。何か聞きたいことあるかな？
@ch c none 1.0

###
### シーン3 (屋上で質問を受け付ける)
###

:LOOP1
@select L1_ADVANTAGE L1_ENV L1_NEXT 何が強みなの？ 動作環境は？ 次へ

:L1_ADVANTAGE
Suika 2の強みは、さまざまな環境で動作することです。
第一に、WindowsアプリとMacアプリが用意されており、どちらでも同じように動きます。
ゲーム開発者は、ユーザの利用しているOSを気にする必要がありません。
第二に、GPUの互換性による問題が発生しません。
画面への描画はCPUによって高速に処理されます。利用しているCPUに合わせた特別なチューニングが行われます。
第三に、まだユーザが少ないことから、作者のサポートを無償で得られます。
Suika 2を利用してくださるゲーム開発者様がいらっしゃいましたら、作者はよろこんでお手伝いします。
@set $1 = 1
@ch c 001-fun.png 1.0
*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*016.ogg*ほかにも聞きたいことある？
@ch c none 1.0
@goto LOOP1

:L1_ENV
Windowsアプリでは、Windows 8.1以降で動くことを確認しています。最新のWindows 10タブレットにも対応しています。
Macアプリでは、Mac OS X 10.9以降で動作することを確認しています。最新のBig SurやM1チップにも対応しています。
さらにいうと、Linuxでも動きます。Suika 2はほとんどLinux上で開発されています。
@set $2 = 1
@ch c 001-fun.png 1.0
*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*018.ogg*まだまだ質問してほしいってさ。
@ch c none 1.0
@goto LOOP1

:L1_NEXT

###
### シーン4 (浜で質問を受け付ける)
###

@ch c 001-fun.png 1.0
*みどり*019.ogg*学校出て、ちょっと歩こうか。どこに行く？浜の方に出てみようか。

@vol bgm 0.0 1.0
@bg coast.png 1.0 c
@vol bgm 1.0 0.0
@bgm coast.ogg
@ch c 001-happy.png 1.0
*みどり*020.ogg*ほかにも知りたいこととかある？
@ch c none 1.0

:LOOP2
@select L2_OLD L2_CUSTOMIZE L2_NEXT Suika1もあるの？ カスタマイズできるの？ 次へ

:L2_OLD
2.0.0より前のバージョンは、西瓜Studioというソフトでした。\n2001年から2003年にかけて、作者が高校生の頃に開発・公開されていました。
2.0.0以降は、エディタの機能を削ったため、Studioの名前を外しました。
西瓜Studio Ver.0.47.4が、ベクター様のサイトにまだ残っているので、興味があれば探してみて下さい。
@set $3 = 1
@ch c 001-fun.png 1.0
*みどり*015.ogg*へー、そうだったんだ。私も知らなかったよ。
*みどり*018.ogg*まだまだ質問してほしいってさ。
@ch c none 1.0
@goto LOOP2

:L2_CUSTOMIZE
フォントファイル、メッセージボックス、セーブ画面など、基本的にはすべてがカスタマイズ可能な設計になっています。
もし、カスタマイズしたいけどできない、という項目があれば、作者までご連絡ください。
@set $4 = 1
@ch c 001-fun.png 1.0
*みどり*017.ogg*うんうん、そうなんだよね。
*みどり*016.ogg*ほかにも聞きたいことある？
@ch c none 1.0
@goto LOOP2

:L2_NEXT

###
### シーン5 (浜でラスト・セリフとモノローグの交錯)
###

@ch c 001-fun.png 1.0
*みどり*021.ogg*今日はいろいろ聞いてくれてありがとうね。

@vol bgm 0.5 1.0
@ch c none 1.0
*みどり*022.ogg*（君に、Suika 2のことを知ってもらいたくて……）

@vol bgm 1.0 1.0
@ch c 001-fun.png 1.0
*みどり*023.ogg*何かわからないことがあったら、いつでも midori@luxion.jp までメールしてよ。

@vol bgm 0.5 1.0
@ch c none 1.0
*みどり*024.ogg*（……本当にメールしていいんだからね？　メールしてくれるかな？）

@vol bgm 0.0 1.0
@ch c 001-fun.png 1.0
*みどり*025.ogg*それじゃ、また会おうね！

###
### シーン6 (おまけ)
###

@if $1 != 1 END
@if $2 != 1 END
@if $3 != 1 END
@if $4 != 1 END
全部読んでいただいてありがとうございました。

###
### 終了ロゴ
###

:END
@bg white.png 1.0
@ch c luxion.png 0.5
@wait 1.0
@ch c none 1.0
